fmod EXPRESSION is
    pr MEMORY .
    pr CONVERSION .

    sort Expression .
    subsort Type < Expression .

    op _+_ : Expression Expression -> Expression [ctor assoc comm] .
    op _-_ : Expression Expression -> Expression [ctor] .
    op _*_ : Expression Expression -> Expression [ctor assoc comm] .
    op size(_) : Type -> Expression [ctor] .
    op get(_,_) : Expression Int -> Expression [ctor] .
    op remove(_,_) : Expression Int -> Expression [ctor] .
    op add(_,_) : Expression Type -> Expression [ctor] .
    op modify(_,_,_) : Expression Int Expression -> Expression [ctor] .
    op toInt(_) : Expression -> Expression [ctor] .
    op toString(_) : Expression -> Expression [ctor] .
    op toBool(_) : Expression -> Expression [ctor] .
    op toFloat(_) : Expression -> Expression [ctor] .
        
    vars T T' : Type .
    vars I I' : Int .
    vars F F' : Float .
    vars S S' : String .
    var B : Bool .
    var M : Memory .
    var Q : Qid .
    vars E E' : Expression .
    var L : ListType .

    op eval : Memory Expression -> Type .
    eq eval (M, (intType I) + (intType I')) = (intType I + I') .
    eq eval (M, (intType I) - (intType I')) = (intType I - I') .
    eq eval (M, (intType I) * (intType I')) = (intType I * I') .

    eq eval (M, (floatType F) + (floatType F')) = (floatType F + F') .
    eq eval (M, (floatType F) - (floatType F')) = (floatType F - F') .
    eq eval (M, (floatType F) * (floatType F')) = (floatType F * F') .

    eq eval (M, (stringType S) + (stringType S')) = (stringType S + S') .

    eq eval (M, Q) = find(M, Q) .
    eq eval (M, T) = T .

    eq eval (M, E + E') = eval(M, eval(M, E) + eval(M, E')) .
    eq eval (M, E - E') = eval(M, eval(M, E) - eval(M, E')) .
    eq eval (M, E * E') = eval(M, eval(M, E) * eval(M, E')) .

    eq eval (M, size(listType L)) = (intType sizeList(L)) .
    eq eval (M, size(Q)) = (intType sizeAux(find(M, Q))) .

    eq eval (M, modify((listType L), I, E)) = (listType modifyElementList(L, eval(M,E), I, 0)) .
    eq eval (M, modify(Q, I, E)) = (listType modifyAux(M, find(M, Q), eval(M,E), I)) .
    eq eval (M, modify(E, I, E')) = eval(M, modify(eval(M, E), I, eval(M,E'))) .

    eq eval (M, get(listType L, I)) = elementList(L, I, 0) .
    eq eval (M, get(Q, I)) = elementListAux(find(M, Q), I) .
    eq eval (M, get(E, I)) = eval(M, get(eval(M,E), I)) .

    eq eval (M, remove(listType L, I)) = (listType removeElementList(L, I, 0)) .
    eq eval (M, remove(Q, I)) = (listType removeElementAux(find(M, Q), I)) .
    eq eval (M, remove(E, I)) = eval(M, remove(eval(M,E), I)) .

    eq eval (M, add(listType L , T)) = (listType L, T) .
    eq eval (M, add(Q, T)) = (listType addAux(find(M, Q), T)) .


    eq eval (M, toString((stringType S))) = string(S) .
    eq eval (M, toString((intType I))) = string(string(I, 10)) .
    eq eval (M, toString((floatType F))) = string(string(F)) .
    eq eval (M, toString((boolType true))) = string("true") .
    eq eval (M, toString((boolType false))) = string("false") .
    eq eval (M, toString(E)) = eval(M, toString(eval(M, E))) .


    eq eval (M, toInt((intType I))) = int(I) .
    eq eval (M, toInt((stringType S))) = int(rat(S, 10)) .
    eq eval (M, toInt((boolType true))) = int(1) .
    eq eval (M, toInt((boolType false))) = int(0) .
    --- eq eval (M, toInt((floatType F))) = int(rat(F)) . TODO
    eq eval (M, toInt(E)) = eval(M, toInt(eval(M, E))) .

    eq eval (M, toFloat((intType I))) = float(float(I)) .
    eq eval (M, toFloat((stringType S))) = float(float(S)) .
    eq eval (M, toFloat((boolType true))) = float(1.0) .
    eq eval (M, toFloat((boolType false))) = float(0.0) .
    eq eval (M, toFloat((floatType F))) = float(F) .
    eq eval (M, toFloat(E)) = eval(M, toFloat(eval(M, E))) .


    ceq eval (M, toBool((intType I))) = bool(false) 
        if I == 0 .
    ceq eval (M, toBool((intType I))) = bool(true) 
        if I > 0 .
    ceq eval (M, toBool((floatType F))) = bool(false) 
        if F == 0.0 .
    ceq eval (M, toBool((floatType F))) = bool(true) 
        if F > 0.0 . 
    ceq eval (M, toBool((stringType S))) = bool(false)
        if S == "" .
    ceq eval (M, toBool((stringType S))) = bool(true)
        if S =/= "" .
    eq eval (M, toBool((boolType B))) = bool(B) .
    eq eval (M, toBool(E)) = eval(M, toBool(eval(M, E))) .

    op removeElementAux : Type Int -> ListType .
    eq removeElementAux((listType L), I) = removeElementList(L, I, 0) .
    eq removeElementAux(T, I) = (intType 0) [owise] .

    op elementListAux : Type Int -> ListType .
    eq elementListAux((listType L), I) = elementList(L, I, 0) .
    eq elementListAux(T, I) = (intType 0) [owise] .

    op sizeAux : Type -> Int .
    eq sizeAux((listType L)) = sizeList(L) .
    eq sizeAux(T) = 0 [owise] .

    op addAux : Type Type -> ListType .
    eq addAux((listType L), T) = L, T .
    eq addAux(T, T') = (intType 0) [owise] .

    op modifyAux : Memory Type Type Int -> ListType .
    eq modifyAux(M, (listType L), T, I) = modifyElementList(L, T, I, 0) .
    eq modifyAux(M, T, T', I) = (intType 0) [owise] .

endfm