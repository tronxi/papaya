mod SEMANTICS is
    pr SYNTAX .
    pr IO .

    sort System .
    subsort System < Attribute .

    op {_|_} : Memory Function -> System [ctor] .

    op System : -> Cid . 
    op system : -> Oid . 

    var M : Memory .
    vars Q Q' : Qid .
    var E : Expression .
    var P P' : Program .
    var S : String .
    vars F F' : Function .
    var R : Return .
    var AL : ArgumentList .
    var CAL : CallArgumentList .

    rl [printFinal] :
        { M | F function 'main() { end } }
        =>
        { M | F function 'main() { print(""); finalPrint } } .

    rl [comment] :
        {M | F function 'main() { // P' P } }
        =>
        {M | F function 'main() { P } } . 

    rl [assignment] :
        {M | F function 'main() { (Q = E); P} }
        =>
        {save(M, Q, eval(M, E)) | F function 'main() { P } } .
    
    rl [printString] :
        < system : System | {M | F function 'main() { print(S); P } } >
        =>
        < system : System | { M | F function 'main() { P }} > print(S, system) .

    rl [printlnString] :
        < system : System | {M | F function 'main() { println(S); P } } >
        =>
        < system : System | { M | F function 'main() { P } } > println(S, system) .
    
    rl [printType] :
        < system : System | {M | F function 'main() { print(E); P } } >
        =>
        < system : System | { M | F function 'main() { P } } > print(M, E, system) .

    rl [printlnType] :
        < system : System | {M | F function 'main() { println(E); P } } >
        =>
        < system : System | { M | F function 'main() { P } } > println(M, E, system) .
    
    crl [callVoidFunction] :
        { M | F function 'main() {{Q CAL}; P } function Q AL {P'} }
        =>
        { evalueArgument(M, AL, CAL) | F function 'main() {P' P } function Q AL {P'} } 
        if sameNumArguments(CAL, AL) .

    crl [assignVoidFunction] :
        { M | F function 'main() {(Q' = {Q CAL}); P } function Q AL {P'} }
        =>
        { evalueArgument(M, AL, CAL)  | F function 'main() {P' P } function Q AL {P'} }
        if sameNumArguments(CAL, AL) .


    crl [callReturnFunction] :
        { M | F function 'main() {{Q CAL}; P } function Q AL {P' # R } }
        =>
        { evalueArgument(M, AL, CAL)  | F function 'main() {P' P } function Q AL {P' # R} }
        if sameNumArguments(CAL, AL) .

    crl [callOnlyReturnFunction] :
        { M | F function 'main() {{Q CAL}; P } function Q AL { # R } }
        =>
        { evalueArgument(M, AL, CAL)  | F function 'main() { P } function Q AL { # R} }
        if sameNumArguments(CAL, AL) .

    crl [callAssignReturnFunction] :
        { M | F function 'main() {(Q' = {Q CAL} ); P } function Q AL {P' # return E ;} }
        =>
        { evalueArgument(M, AL, CAL)  | F function 'main() {P' return Q' -> E ; P } function Q AL {P' # return E ;} }
        if sameNumArguments(CAL, AL) .

    crl [callAssignOnlyReturnFunction] :
        { M | F function 'main() {(Q' = {Q CAL}); P } function Q AL { # return E ;} }
        =>
        { evalueArgument(M, AL, CAL)  | F function 'main() { return Q' -> E ; P } function Q AL { # return E ;} }
        if sameNumArguments(CAL, AL) .
    
    rl [assignReturn] :
        { M | F function 'main() { return Q -> E ; P } } 
        =>
        { save(M, Q, eval(M, E)) | F function 'main() { P } } .

endm